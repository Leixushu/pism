/* -*- mode: maxima -*- */
assume(dx > 0, dy > 0);

/* Derivation of the Jacobian of the map from the reference P1 element
to physical elements embedded in a Q1 element aligned with the x and y
axes. */

/* Coordinates of the nodes of a Q1 rectangular element aligned with the coordinate system. */
pts : [
  [-dx/2, -dy/2],
  [ dx/2, -dy/2],
  [ dx/2,  dy/2],
  [-dx/2,  dy/2]]$

/* Short-cuts for nodes */
P0 : pts[1]$
P1 : pts[2]$
P2 : pts[3]$
P3 : pts[4]$

/* Four triangles embedded in a rectangular element */
tris : [
  [P0, P1, P3],
  [P1, P2, P0],
  [P2, P3, P1],
  [P3, P0, P2]
]$

/* P1 shape functions, using the reference element defined above */
chi[1](xi,eta) := 1 - xi - eta$
chi[2](xi,eta) := xi$
chi[3](xi,eta) := eta$

/* Map from the reference element. */
x(xi,eta) := 'sum(x[j]*chi[j](xi,eta), j, 1, 3)$
y(xi,eta) := 'sum(y[j]*chi[j](xi,eta), j, 1, 3)$

J : matrix(
  ['diff(x(xi,eta), xi), 'diff(y(xi,eta), xi)],
  ['diff(x(xi,eta), eta),'diff(y(xi,eta), eta)])$

/* Compute the Jacobian of the mapping from the reference element */
J_s : ratsimp(ev(J, nouns))$

eqs[k] := block([T],
  T : tris[k],
  [
  x[1] = T[1][1],
  y[1] = T[1][2],
  x[2] = T[2][1],
  y[2] = T[2][2],
  x[3] = T[3][1],
  y[3] = T[3][2]
  ])$

J[k] := ev(J_s, eqs[k]);

J_det[k] := determinant(J[k]);

J_inv[k] := invert(J[k]);

J[1];
J[2];
J[3];
J[4];

/* boundary integrals over sides of P1 elements */

/* magnitude of a 2D vector */
magnitude(v) := sqrt(v[1]*v[1] + v[2]*v[2]);

/* map from [-1, 1] to [0, 1] */
L(t) := 1/2 * (t + 1);

/* xi, eta coordinates of the nodes of the reference element */
P : [[0, 0], [1, 0], [0, 1]];

/* parameterization of sides of the reference element */
r_star[i](t) := (1 - L(t)) * P[i] + L(t) * P[mod(i, 3) + 1];

xx[i](t) := sum(x[j]*apply(chi[j], r_star[i](t)), j, 1, 3)$
yy[i](t) := sum(y[j]*apply(chi[j], r_star[i](t)), j, 1, 3)$

r[i](t) := [xx[i](t), yy[i](t)];

dr[i] := ev(diff(r[i](t), t), nouns);

drm[i,j] := subst(eqs[i], magnitude(dr[j]));

