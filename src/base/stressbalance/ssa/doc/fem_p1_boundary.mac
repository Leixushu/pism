/* -*- mode: maxima -*- */
assume(dx > 0, dy > 0);

/* Computing quadrature weights for integrating over sides of P1
triangular elements embedded in Q1 elements that are aligned with
coordinate axes. */

/* Coordinates of the nodes of a Q1 rectangular element aligned with
the coordinate system. */

pts : [
  [-dx/2, -dy/2],
  [ dx/2, -dy/2],
  [ dx/2,  dy/2],
  [-dx/2,  dy/2]]$

/* Short-cuts for nodes */
P0 : pts[1]$
P1 : pts[2]$
P2 : pts[3]$
P3 : pts[4]$

/* Four triangles embedded in a rectangular element */
triangles : [
  [P0, P1, P3],
  [P1, P2, P0],
  [P2, P3, P1],
  [P3, P0, P2]
]$

/* Equations for coordinates of the nodes of physical P1 elements. */
eqs[k] := block([T],
  T : triangles[k],
  [
  x[1] = T[1][1],
  y[1] = T[1][2],
  x[2] = T[2][1],
  y[2] = T[2][2],
  x[3] = T[3][1],
  y[3] = T[3][2]
  ])$

/* xi, eta coordinates of the nodes of the reference element */
P : [[0, 0], [1, 0], [0, 1]];

/* P1 shape functions defined on the reference element defined above */
chi[1](xi,eta) := 1 - xi - eta$
chi[2](xi,eta) := xi$
chi[3](xi,eta) := eta$

/* magnitude of a 2D vector */
magnitude(v) := sqrt(v[1]*v[1] + v[2]*v[2]);

/* map from [-1, 1] to [0, 1] */
L(t) := 1/2 * (t + 1);

/* parameterization of sides of the reference element, t \in [-1, 1]. */
r_star[i](t) := (1 - L(t)) * P[i] + L(t) * P[mod(i, 3) + 1];

xx[i](t) := sum(x[j]*apply(chi[j], r_star[i](t)), j, 1, 3)$
yy[i](t) := sum(y[j]*apply(chi[j], r_star[i](t)), j, 1, 3)$

/* parameterization of the i-th side of a physical element */
r[i](t) := [xx[i](t), yy[i](t)];

dr[i] := diff(r[i](t), t);

drm[i,j] := subst(eqs[i], magnitude(dr[j]));

